# Maintainer: Brendan Forster <brendan@github.com>

_realname="git-lfs"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.0
pkgrel=1
pkgdesc="An open source Git extension for versioning large files"
install=git-lfs.install
arch=('any')
url="https://github.com/git-lfs/git-lfs"
src_zip_url="${url}/archive/v${pkgver}.zip"
license=('MIT')
groups=('VCS')

case "$CARCH" in
i686)
  folder="git-lfs-windows-386-$pkgver"
  sha256sum=b9330e8dfc952a0916fd35450b27235a2a8bb9add7e837ab948c16aaf76498d3
  ;;
x86_64)
  folder="git-lfs-windows-amd64-$pkgver"
  sha256sum=88747aa174a748b5a9559d5bc5845cb7ed60dfc240b32b3e4f230c52692a7f6b
  ;;
esac

source=("https://github.com/github/git-lfs/releases/download/v$pkgver/$folder.zip"
	"$src_zip_url")

# Git LFS' source .zip now contains symbolic links that bsdtar does not like...
noextract=("${src_zip_url##*/}")

sha256sums=("$sha256sum" SKIP)
options=('!strip')

package() {
  install -d -m755 $pkgdir/$MINGW_PREFIX/bin
  install -m755 $srcdir/git-lfs.exe $pkgdir/$MINGW_PREFIX/bin/git-lfs.exe
}
