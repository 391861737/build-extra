# Maintainer: Brendan Forster <brendan@github.com>

_realname="git-lfs"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.1.0
pkgrel=1
pkgdesc="An open source Git extension for versioning large files"
install=git-lfs.install
arch=('any')
url="https://github.com/git-lfs/git-lfs"
src_zip_url="${url}/archive/v${pkgver}.zip"
license=('MIT')
groups=('VCS')

case "$CARCH" in
i686)
  folder="git-lfs-windows-386-$pkgver"
  sha256sum=970d6a9abbfbf37b6f5d67663403808a2ac9e519f59c5eb923d753186e7e18ca
  ;;
x86_64)
  folder="git-lfs-windows-amd64-$pkgver"
  sha256sum=a40adc489a99d1b7809aab89fa78b6dee2399af6542e4b9faae552a7a248c1ce
  ;;
esac

source=("https://github.com/github/git-lfs/releases/download/v$pkgver/$folder.zip"
	"$src_zip_url")

# Git LFS' source .zip now contains symbolic links that bsdtar does not like...
noextract=("${src_zip_url##*/}")

sha256sums=("$sha256sum" SKIP)
options=('!strip')

package() {
  install -d -m755 $pkgdir/$MINGW_PREFIX/bin
  install -m755 $srcdir/$folder/git-lfs.exe $pkgdir/$MINGW_PREFIX/bin/git-lfs.exe
}
