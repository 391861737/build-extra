# Maintainer: nalla <nalla@hamal.uberspace.de>

pkgname=('git-extra')
_ver_base=1.1
pkgver=1.1.28.1fa1576
pkgrel=1
pkgdesc="Git for Windows extra files"
arch=('i686' 'x86_64')
url="https://github.com/git-for-windows/build-extra"
license=('GPL')
groups=('VCS')
optdepends=('vim' 'filesystem')
install='git-extra.install'
pkgver() {
  printf "%s.%s.%s" "${_ver_base}" "$(git rev-list --count HEAD -- "$startdir")" "$(git rev-parse --short HEAD)"
}

build() {
  printf '%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n%s\n' \
    "export LC_ALL=C" \
    "post_install () {" \
    "for dir in mingw32 mingw64; do test ! -d /\$dir || test -f /\$dir/etc/gitconfig || cat > /\$dir/etc/gitconfig <<\\GITCONFIG; done" \
    "$(cat $startdir/gitconfig)" \
    "GITCONFIG" \
    "grep -q '^db_home: windows' /etc/nsswitch.conf ||" \
    "sed -i 's/^\\(db_\\(home\\|shell\\|gecos\\): \\)\\([^w]\\)/\\1windows \\3/' /etc/nsswitch.conf" \
    "! grep -q '^PS1=' /etc/bash.bashrc ||" \
    "sed -i 's/^PS1=/#&/' /etc/bash.bashrc" \
    "grep -q git-for-windows etc/pacman.conf ||" \
    "sed -i -e '/^\[mingw32\]/i\
[git-for-windows]\n\
Server = https://dl.bintray.com/\$repo/pacman/\$arch\n\
SigLevel = Optional\n\
' etc/pacman.conf" \
    "test i686 != \$(uname -m) ||" \
    "case \"\$(md5sum.exe < /msys2.ico)\" in" \
    "292ad5cd*) cp /usr/share/git/msys2-32.ico /msys2.ico;;" \
    "esac" \
    "test ! -f /etc/post-install/05-home-dir.post ||" \
    "rm /etc/post-install/05-home-dir.post" \
    "}" \
    "post_upgrade () {" \
    "    post_install" \
    "}" > $startdir/$pkgname.install

    gcc -o create-shortcut.exe $startdir/create-shortcut.c -luuid -lole32
}

package() {
  case "$CARCH" in
    i686)
      mingwdir="mingw32"
    ;;
    x86_64)
      mingwdir="mingw64"
    ;;
  esac

  install -d -m755 $pkgdir/etc/profile.d
  install -d -m755 $pkgdir/usr/bin
  install -d -m755 $pkgdir/usr/share/git
  install -d -m755 $pkgdir/$mingwdir/bin
  install -m644 $startdir/inputrc $pkgdir/etc
  install -m644 $startdir/vimrc $pkgdir/etc
  install -m755 $startdir/vi $pkgdir/usr/bin
  install -m755 $startdir/wordpad $pkgdir/usr/bin
  install -m755 create-shortcut.exe $pkgdir/$mingwdir/bin
  install -m755 $startdir/git-prompt.sh $pkgdir/etc/profile.d
  install -m755 $startdir/aliases.sh $pkgdir/etc/profile.d
  install -m755 $startdir/env.sh $pkgdir/etc/profile.d
  install -m644 $startdir/msys2-32.ico $pkgdir/usr/share/git
}
